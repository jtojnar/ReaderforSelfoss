buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

apply plugin: 'io.fabric'

apply plugin: 'kotlin-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "apps.amine.bou.readerforselfoss"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1507
        versionName "1.5.0.7"

        // Enabling multidex support.
        multiDexEnabled true
        lintOptions {
            abortOnError true
            disable 'InvalidPackage'
        }
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    flavorDimensions "build"
    productFlavors {
        githubConfig {
            versionNameSuffix '-github'
            dimension "build"
            buildConfigField "boolean", "GITHUB_VERSION", "true"
        }
        storeConfig {
            versionNameSuffix '-store'
            dimension "build"
            buildConfigField "boolean", "GITHUB_VERSION", "false"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Android Support
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:support-vector-drawable:25.3.1'
    compile 'com.android.support:customtabs:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Firebase + crashlytics
    compile 'com.google.firebase:firebase-core:10.2.6'
    compile 'com.google.firebase:firebase-config:10.2.6'
    compile 'com.google.firebase:firebase-invites:10.2.6'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

    //multidex
    compile 'com.android.support:multidex:1.0.1'

    // Intro
    compile 'agency.tango.android:material-intro-screen:0.0.5'

    // About
    compile('com.mikepenz:aboutlibraries:5.9.6@aar') {
        transitive = true
    }

    // Retrofit + http logging + okhttp
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.burgstaller:okhttp-digest:1.12'

    // Material-ish things
    compile 'com.roughike:bottom-bar:2.3.1'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.github.jd-alexander:LikeButton:0.2.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    compile 'org.sufficientlysecure:html-textview:3.3'

    // glide
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Asking politely users to rate the app
    compile 'com.github.stkent:amplify:1.5.0'

    // For the article reader
    compile 'com.klinkerapps:drag-dismiss-activity:1.4.0'

    // Drawer
    compile('com.mikepenz:materialdrawer:5.9.2@aar') {
        transitive = true
    }
    compile 'com.anupcowkur:reservoir:3.1.0'

}

apply plugin: 'com.google.gms.google-services'


afterEvaluate {
    initFabricPropertiesIfNeeded()
}

def initFabricPropertiesIfNeeded() {
    def propertiesFile = file('fabric.properties')
    if (!propertiesFile.exists()) {
        def commentMessage = "This is autogenerated fabric property from system environment to prevent key to be committed to source control."
        ant.propertyfile(file: "fabric.properties", comment: commentMessage) {
            entry(key: "apiSecret", value: crashlyticsdemoApisecret)
            entry(key: "apiKey", value: crashlyticsdemoApikey)
        }
    }
}